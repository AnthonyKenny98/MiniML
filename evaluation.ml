(* 
                         CS 51 Final Project
                         MiniML -- Evaluation
                             Spring 2017
*)

(* This module implements a small untyped ML-like language under
   various operational semantics.
 *)
    
open Expr ;;
open Printf;;
  
(* Exception for evaluator runtime, generated by a runtime error *)
exception EvalError of string ;;
(* Exception for evaluator runtime, generated by an explicit "raise" construct *)
exception EvalException;;


(* Environments and values *)

module type Env_type = sig
    type env
    type value =
      | Val of expr
      | Closure of (expr * env)
    val create : unit -> env
    val close : expr -> env -> value
    val lookup : env -> varid -> value
    val extend : env -> varid -> value ref -> env
    val env_to_string : env -> string
    val value_to_string : ?printenvp:bool -> value -> string
    val value_to_expr : value -> expr
  end

module Env : Env_type =
  struct
    type env = (varid * value ref) list
     and value =
       | Val of expr
       | Closure of (expr * env)

    (* Creates an empty environment *)
    let create () : env = [] ;;

    (* Creates a closure from an expression and the environment it's
       defined in *)
    let close (exp : expr) (env : env) : value =
      Closure(exp, env)
   ;;

    (* Looks up the value of a variable in the environment *)
    let rec lookup (env : env) (varname : varid) : value =
      match env with
      | [] -> raise (EvalError ("Unbound Variable: " ^ varname))
      | h :: t -> let var, valref = h in 
        if var = varname then !valref else lookup t varname;;

    (* Returns a new environment just like env except that it maps the
       variable varid to loc *)
    let extend (env : env) (varname : varid) (loc : value ref) : env =
      (varname, loc) :: env ;;

    (* Returns a printable string representation of a value; the flag
       printenvp determines whether to include the environment in the
       string representation when called on a closure *)
    let rec value_to_string ?(printenvp : bool = true) (v : value) : string =
      match v with
      | Val exp -> sprintf "Val (%s)" (exp_to_string exp)
      | Closure (exp, env) -> 
          let envp = if printenvp then 
            List.fold_left (fun _ (var, varref) -> sprintf "(%s : %s)" var (value_to_string !varref)) "" env
            else "env" in
            sprintf "Closure (%s %s)" (exp_to_string exp) envp;;

   (* Returns a printable string representation of an environment *)
   let env_to_string (env : env) : string =
      List.fold_left (fun _ (var, varref) -> sprintf "(%s : %s)" var (value_to_string !varref)) "" env;;

   let value_to_expr (value : value) : expr = 
    match value with 
    | Val expr | Closure (expr, _)->  expr;;

  end
;;
  
(* The evaluation function: Returns the result of type `value` of
   evaluating the expression `exp` in the environment `env`. In this
   initial implementation, we just convert the expression unchanged to
   a value and return it. *)


(** The external evaluator, which can be either the identity function,
    the substitution model version or the dynamic or lexical
    environment model version. *)

let exprs = Env.value_to_expr;;

let eval_x ?(subst = false) (env : Env.env) (exp : expr) (eval : Env.env -> expr -> Env.value)  = 
  match exp with
  | Unop (unop, e) -> Env.Val(Unop(unop, (exprs (eval env e))))
  | Binop (binop, e1, e2) -> 
    (match (exprs (eval env e1)), (exprs(eval env e2)) with
    | Num x, Num y -> 
      (match binop with
      | Plus     -> Env.Val(Num (x + y))
      | Minus    -> Env.Val(Num (x - y))
      | Times    -> Env.Val(Num (x * y))
      | Equals   -> Env.Val(Bool (x = y))
      | LessThan -> Env.Val(Bool (x < y)))
    | _,_ -> raise (EvalError ("Invalid Binop Expression: " ^ 
            (exp_to_string (Binop(binop, exprs (eval env e1), exprs (eval env e2)))))))
  | Conditional (condition, e1, e2) -> 
      (match exprs (eval env condition) with 
      | Bool b -> if b then (eval env e1) else (eval env e2) 
      | _ -> raise (EvalError ("Invalid Condition: " ^ (exp_to_string condition)))) 
  | _ -> if subst then Env.Val(exp) else (match exp with 

  | Var x -> (match Env.lookup env x with 
            | Val y | Closure (y, _) -> Env.Val(y))  
  | Let (v, e1, e2) -> eval (Env.extend env v (ref (eval env e1))) e2
  | Letrec (v, e1, e2) -> eval (Env.extend env v (ref (eval (Env.extend env v (ref (Env.Val(Unassigned)))) e1))) e2
  | App _ -> eval env exp
  | Num _ | Unassigned | Raise | Bool _ -> Env.Val(exp)
  | _ -> eval env exp)

let eval_t _env exp = exp ;;

let rec eval_s env exp : Env.value = 
  let eval_s' = eval_s env in
  match exp with 
  | Var x -> raise (EvalError ("Unbound Variable: " ^ x))
  | Let (v, e1, e2) -> eval_s' (subst v e1 e2)
  | Letrec (x, v, p) -> eval_s' (subst x (subst x (Letrec(x, v, Var(x))) v) p)
  | App (f, e2) -> 
    (match exprs (eval_s' f) with
    | Fun (x, p) -> eval_s' (subst x e2 p) 
    | _ -> raise (EvalError (sprintf "This cannot be applied: %s is not a function" (exp_to_string f))))
  | _ -> eval_x env exp eval_s ~subst:true


let rec eval_d env exp : Env.value =
match exp with 
  | App (f, v) -> 
    (match exprs (eval_d env f) with
    | Fun (x, e) -> eval_d (Env.extend env x (ref (eval_d env v))) e
    | _ -> raise (EvalError (sprintf "This cannot be applied: %s is not a function" (exp_to_string f))))
  | Fun (_) -> Val(exp)
  | _ -> eval_x env exp eval_d
;;

let rec eval_l env exp : Env.value = 
match exp with 
  | App (f, _) -> 
    (match (eval_l env f) with
    | Env.Closure (Fun (_, e), env1) -> eval_l env1 e
    | _ -> raise (EvalError (sprintf "This cannot be applied: %s is not a function" (exp_to_string f))))
  | Fun (_) -> Env.close exp env 
  | _ -> eval_x env exp eval_d
;;



let evaluate = eval_l ;;
